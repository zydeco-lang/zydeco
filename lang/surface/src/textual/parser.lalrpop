use crate::textual::{
    syntax::*, lexer::*, escape,
};

grammar<'input, 'arena>(
    input: &'input str,
    loc: &'input LocationCtx,
    parser: &'arena mut Parser,
);

//* --------------------------------- Tokens --------------------------------- */

extern {
    type Location = usize;
    
    enum Tok<'input> {
        "UpperId" => Tok::UpperIdent(<&'input str>),
        "LowerId" => Tok::LowerIdent(<&'input str>),
        "CtorId" => Tok::CtorIdent(<&'input str>),
        "DtorId" => Tok::DtorIdent(<&'input str>),

        "where" => Tok::Where,
        "end" => Tok::End,
        "pub" => Tok::Public,
        "module" => Tok::Module,
        "layer" => Tok::Layer,
        "root" => Tok::Root,
        "use" => Tok::Use,
        "embed" => Tok::Embed,
        "data" => Tok::Data,
        "codata" => Tok::Codata,
        "alias" => Tok::Alias,
        "define" => Tok::Define,
        "extern" => Tok::Extern,
        "main" => Tok::Exec,
        "let" => Tok::Let,
        "in" => Tok::In,
        "do" => Tok::Do,
        "do~" => Tok::DoTilde,
        "ret" => Tok::Ret,
        "with" => Tok::With,
        "with_mo" => Tok::WithMo,
        "with_alg" => Tok::WithAlg,
        "import" => Tok::Import,
        "inline" => Tok::Inline,
        "begin" => Tok::Begin,
        "fn" => Tok::Fn,
        "pi" => Tok::Pi,
        "fix" => Tok::Fix,
        "match" => Tok::Match,
        "comatch" => Tok::Comatch,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "sigma" => Tok::Sigma,

        "IntLit" => Tok::IntLit(<&'input str>),
        "StrLit" => Tok::StrLit(<&'input str>),
        "CharLit" => Tok::CharLit(<&'input str>),

        "(" => Tok::ParenOpen,
        ")" => Tok::ParenClose,
        "[" => Tok::BracketOpen,
        "]" => Tok::BracketClose,
        "{" => Tok::BraceOpen,
        "}" => Tok::BraceClose,
        "," => Tok::Comma,
        ":" => Tok::Colon,
        "::" => Tok::ColonColon,
        "=" => Tok::Equals,
        ";" => Tok::Semicolon,
        "!" => Tok::Force,
        "/" => Tok::Slash,
        "|" => Tok::Branch,
        "+" => Tok::Plus,
        "*" => Tok::Star,
        "." => Tok::Dot,
        ".." => Tok::DotDot,
        "->" => Tok::Arrow,
        "<-" => Tok::Assign,
        "_" => Tok::Hole,
    }
}

//* -------------------------------- Utilities ------------------------------- */

#[inline]
Sp<T>: Sp<T> = <l:@L> <inner:T> <r:@R> => Sp {
    info: Span::new(l, r).under_loc_ctx(loc), inner,
};

// #[inline]
SepByDelim<Start, Sep, T, End>: Vec<T> = {
    Start <mut v: (<T> Sep)*> <last: T?> End => {
        match last {
            None => { },
            Some(t) => { v.push(t); }
        };
        v
    }
};

//* -------------------------------- Terminals ------------------------------- */

Int: i64 = <"IntLit"> => <>.parse().unwrap();
String: String = <"StrLit"> => {
    escape::apply_string_escapes(&<>[1..(<>).len()-1])
};
Char: char = <"CharLit"> => escape::apply_char_escapes(<>);

Literal: Literal = {
    <Int> => <>.into(),
    <String> => <>.chars().collect::<Vec<_>>().into(),
    <Char> => <>.into(),
};

//* ------------------------------- Identifiers ------------------------------ */

#[inline]
MixedId: String = { <"UpperId"> => <>.into(), <"LowerId"> => <>.into(), };

#[inline]
VarName: VarName = <MixedId> => VarName(<>);
CtorName: CtorName = <"CtorId"> => CtorName(<>.into());
DtorName: DtorName = <"DtorId"> => DtorName(<>.into());

NameDef<T>: NameDef<T> = <T> => NameDef(<>);
PathSeg: VarName = {
    <VarName> => <>.into(),
    // ".." => VarName(String::from("..")),
};
NameRef<T>: NameRef<T> = {
    <root:("root" "/")?> <path:(<VarName> "/")*> <inner:T> => {
        NameRef(root.is_some(), path, inner)
    },
};

DefId: DefId = <NameDef<Sp<VarName>>> => parser.def(<>.0);

//* ----------------------------------- Use ---------------------------------- */

UsePath: UsePath = <NameRef<UseEnum>> => UsePath(<>);
UseEnum: UseEnum = {
    <VarName> => <>.into(),
    <binder:VarName> "=" <origin:VarName> => UseAlias(binder, origin).into(),
    "*" => UseAll.into(),
    <Uses> => <>.into(),
};
Uses: Uses = <SepByDelim<"(", ",", <UsePath>, ")">> => Uses(<>);

//* ------------------------------- Structural ------------------------------- */

Paren<T>: Paren<T> = SepByDelim<"(", ",", <T>, ")"> => Paren(<>);
App<T>: App<T> = <T*> => Appli(<>);
Ann<Tm, Ty>: Ann<Tm, Ty> = <tm:Tm> ":" <ty:Ty> => {
    Ann { tm, ty, }
};
Hole: Hole = "_" => Hole;

//* --------------------------------- Pattern -------------------------------- */

#[inline]
PatternGen<P>: PatId = <Sp<P>> => parser.pat(<>);

#[inline]
PatId: PatId = <PatternGen<Pattern>>;
Pattern: Pattern = {
    "(" <Ann<PatId, TermId>> ")" => <>.into(),
    <Hole> => <>.into(),
    <DefId> => <>.into(),
    <ctor:CtorName> <pat:PatId> => {
        Ctor(ctor, pat).into()
    },
    <Paren<PatId>> => <>.into(),
};

#[inline]
CoPatternGen<P>: CoPatId = <Sp<P>> => parser.copat(<>);

#[inline]
CoPatId: CoPatId = <CoPatternGen<CoPattern>>;
CoPattern: CoPattern = {
    <CoPatternAtom>,
    <x:CoPatId> <y:CoPatternAtomId> => {
        Appli(vec![x, y]).into()
    },
};

#[inline]
CoPatternAtomId: CoPatId = <CoPatternGen<CoPatternAtom>>;
CoPatternAtom: CoPattern = {
    <PatId> => <>.into(),
    <DtorName> => {
        <>.into()
    },
};

//* ---------------------------------- Term ---------------------------------- */

CompModifier: () = { "!" };

GenBind<T>: GenBind<T> = {
    <comp:CompModifier?> <fix:"fix"?> <binder:PatId> <params:CoPatId?>
    <ty:(":" <TermId>)?> <bindee:T> => GenBind {
        fix: fix.is_some(),
        comp: comp.is_some(),
        binder, params, ty, bindee,
    }
};
GenBindTerm: GenBind<TermId> = GenBind<("=" <TermId>)>;
GenBindNull: GenBind<()> = GenBind<()>;

Data: Data = <DataArm*> => Data { arms: <> };
#[inline]
DataArm: DataArm = {
    "|" <name:CtorName> ":" <param:TermId> => {
        DataArm { name, param }
    },
};
CoData: CoData = <CoDataArm*> => CoData { arms: <> };
#[inline]
CoDataArm: CoDataArm = {
    "|" <name:DtorName> <params:CoPatId?> ":" <out: TermId> => {
        CoDataArm { name, params, out }
    },
};

Matcher: Matcher<PatId, TermId> = {
    "|" <binder:PatId> "->" <tail:TermId> => Matcher {
        binder, tail
    },
};
CoMatcher: CoMatcherParam = {
    "|" <params:CoPatId> "->" <tail:TermId> => CoMatcherParam {
        params, tail
    },
};

#[inline]
TermGen<T>: TermId = <Sp<T>> => parser.term(<>);

#[inline]
TermId: TermId = <TermGen<Term>>;
Term: Term = {
    <TermArrow>,
    // abstraction
    "fn" <params:CoPatId> "->" <term:TermId> => {
        Abs(params, term)
    }.into(),
    "comatch" <params:CoPatId> "->" <term:TermId> "end" => {
        Abs(params, term)
    }.into(),
    // pi
    "pi" <params:CoPatId> "." <term:TermId> => {
        Pi(params, term)
    }.into(),
    // forall
    "forall" <params:CoPatId> "." <ty:TermArrowId> => {
        Forall(params, ty)
    }.into(),
    // sigma
    "sigma" <params:CoPatId> "." <ty:TermArrowId> => {
        Sigma(params, ty)
    }.into(),
    // exists
    "exists" <params:CoPatId> "." <ty:TermArrowId> => {
        Exists(params, ty)
    }.into(),
    // fixed point
    "fix" <binder:PatId> "->" <term:TermId> => {
        Fix(binder, term)
    }.into(),
    // kont call
    "do~" <body:TermId> ";" <tail:TermId> => KontCall {
        body, tail
    }.into(),
    // bind
    "do" <binder:PatId> "<-" <bindee:TermId> ";" <tail:TermId> => Bind {
        binder, bindee, tail
    }.into(),
    // pure bind
    "let" <binding:GenBindTerm> "in" <tail:TermId> => GenPureBind {
        binding, tail
    }.into(),
    // // use bind
    // "use" <uses:UsePath> "in" <tail:TermId> => UseBind {
    //     uses, tail
    // }.into(),
};

#[inline]
TermAnnId: TermId = <TermGen<TermAnn>>;
TermAnn: Term = {
    <Term>,
    <Ann<TermId, TermId>> => <>.into(),
};

#[inline]
TermParenId: TermId = <TermGen<TermParen>>;
TermParen: Term = <Paren<TermAnnId>> => <>.into();

#[inline]
TermArrowId: TermId = <TermGen<TermArrow>>;
TermArrow: Term = {
    <TermProd>,
    // arrow
    <ty_in:TermProdId> "->" <ty_out:TermArrowId> => {
        Arrow(ty_in, ty_out).into()
    },
};

#[inline]
TermProdId: TermId = <TermGen<TermProd>>;
TermProd: Term = {
    <TermApp>,
    // product
    <ty_l:TermAppId> "*" <ty_r:TermProdId> => {
        Prod(ty_l, ty_r).into()
    },
};

#[inline]
TermAppId: TermId = <TermGen<TermApp>>;
TermApp: Term = {
    <TermAtom>,
    // application
    <term:TermAppId> <arg:TermAtomId> => {
        Appli(vec![term, arg]).into()
    },
    // destructor
    <term:TermAppId> <name:DtorName> => {
        Dtor(term, name).into()
    },
};

#[inline]
TermAtomId: TermId = <TermGen<TermAtom>>;
TermAtom: Term = {
    // parentheses
    <TermParen>,
    <Hole> => <>.into(),
    <NameRef<VarName>> => <>.into(),
    // thunk & force
    "{" <TermId> "}" => Thunk(<>).into(),
    "!" <TermAtomId> => Force(<>).into(),
    // ret
    "ret" <TermAtomId> => Ret(<>).into(),
    // monadic block
    "begin" <TermId> "end" => MoBlock(<>).into(),
    // structural types
    "data" <Data> "end" => <>.into(),
    "codata" <CoData> "end" => <>.into(),
    // constructor
    <name:CtorName> <args:TermAtomId> => {
        Ctor(name, args).into()
    },
    // match
    "match" <scrut:TermId> <arms:(<Matcher>)*> "end" => Match {
        scrut, arms
    }.into(),
    // comatch
    "comatch" <arms:(<CoMatcher>)*> "end" => CoMatchParam {
        arms
    }.into(),
    <Literal> => <>.into(),
};

pub SingleTerm: TermId = TermId;

//* -------------------------------- TopLevel -------------------------------- */

DataDef: DataDef = {
    "data" <name:DefId> <params:PatId*>
        <def:("where" <Data>)>
    "end" => DataDef {
        name, params, def,
    }
};

CoDataDef: CoDataDef = {
    "codata" <name:DefId> <params:PatId*>
        <def:("where" <CoData>)>
    "end" => CoDataDef {
        name, params, def,
    }
};

// UseLayer: Modifiers<UsePath> = {
//     <Modifiers<("use" <UsePath>)>> => <>.into(),
// };

#[inline]
DeclarationGen<D>: DeclId = <Sp<D>> => parser.decl(<>);

#[inline]
DeclId: DeclId = <DeclarationGen<Modifiers<Declaration>>>;
Declaration: Declaration = {
    <DataDef> => <>.into(),
    <CoDataDef> => <>.into(),
    "define" <GenBind<("=" <TermId>)?>> "end" => Define(<>).into(),
    "alias" <GenBindTerm> "end" => Alias(<>).into(),
    "module" <name:(<NameRef<VarName>>)?>
    <top:("where" <TopLevel>)> "end" => Module {
        name, top
    }.into(),
    // "layer" <name:(<NameRef<VarName>>)?>
    //     <uses:(<UseLayer>)*>
    // <top:("where" <TopLevel>)> "end" => Layer {
    //     name, uses, top
    // }.into(),
    // "use" <UsePath> "end" => UseDef(<>).into(),
    // "use" <uses:UsePath> "where" <top:TopLevel> "end" => UseBlock {
    //     uses, top
    // }.into(),
    "main" <TermId> "end" => Exec(<>).into(),
};

Modifiers<T>: Modifiers<T> = {
    <public:"pub"?> <external:"extern"?> <inner:T> => Modifiers {
        public: public.is_some(),
        external: external.is_some(),
        inner,
    },
};

pub ReplInput: ReplInput = {
    <DeclId> => <>.into(),
    <TermId> => <>.into(),
};

pub TopLevel: TopLevel = {
    <DeclId*> => TopLevel(<>),
};

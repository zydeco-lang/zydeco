use crate::textual::{
    syntax::*, lexer::*, escape::self,
};

grammar<'input, 'arena>(
    input: &'input str,
    loc: &'input LocationCtx,
    parser: &'arena mut Parser,
);

//* --------------------------------- Tokens --------------------------------- */

extern {
    type Location = usize;
    
    enum Tok<'input> {
        "UpperId" => Tok::UpperIdent(<&'input str>),
        "LowerId" => Tok::LowerIdent(<&'input str>),
        "CtorId" => Tok::CtorIdent(<&'input str>),
        "DtorId" => Tok::DtorIdent(<&'input str>),

        "where" => Tok::Where,
        "end" => Tok::End,
        "pub" => Tok::Public,
        "module" => Tok::Module,
        "root" => Tok::Root,
        "use" => Tok::Use,
        "data" => Tok::Data,
        "codata" => Tok::Codata,
        "alias" => Tok::Alias,
        "define" => Tok::Define,
        "extern" => Tok::Extern,
        "main" => Tok::Exec,
        "let" => Tok::Let,
        "in" => Tok::In,
        "do" => Tok::Do,
        "do~" => Tok::DoTilde,
        "ret" => Tok::Ret,
        "monadic" => Tok::Monadic,
        "monadically" => Tok::Monadically,
        "fn" => Tok::Fn,
        "pi" => Tok::Pi,
        "fix" => Tok::Fix,
        "match" => Tok::Match,
        "comatch" => Tok::Comatch,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "sigma" => Tok::Sigma,

        "IntLit" => Tok::IntLit(<&'input str>),
        "StrLit" => Tok::StrLit(<&'input str>),
        "CharLit" => Tok::CharLit(<&'input str>),

        "(" => Tok::ParenOpen,
        ")" => Tok::ParenClose,
        "[" => Tok::BracketOpen,
        "]" => Tok::BracketClose,
        "{" => Tok::BraceOpen,
        "}" => Tok::BraceClose,
        "," => Tok::Comma,
        ":" => Tok::Colon,
        "::" => Tok::ColonColon,
        "=" => Tok::Equals,
        ";" => Tok::Semicolon,
        "!" => Tok::Force,
        "/" => Tok::Slash,
        "|" => Tok::Branch,
        "+" => Tok::Plus,
        "*" => Tok::Star,
        "." => Tok::Dot,
        ".." => Tok::DotDot,
        "->" => Tok::Arrow,
        "<-" => Tok::Assign,
        "_" => Tok::Hole,
    }
}

//* -------------------------------- Utilities ------------------------------- */

Sp<T>: Sp<T> = <l:@L> <inner:T> <r:@R> => Sp {
    info: Span::new(l, r).under_loc_ctx(loc), inner,
};

SepByDelim<Start, Sep, T, End>: Vec<T> = {
    Start <mut v: (<T> Sep)*> <last: T?> End => {
        match last {
            None => { },
            Some(t) => { v.push(t); }
        };
        v
    }
};

//* -------------------------------- Terminals ------------------------------- */

Int: i64 = <"IntLit"> => <>.parse().unwrap();
String: String = <"StrLit"> => {
    escape::apply_string_escapes(&<>[1..(<>).len()-1])
};
Char: char = <"CharLit"> => escape::apply_char_escapes(<>);

Literal: Literal = {
    <Int> => <>.into(),
    <String> => <>.chars().collect::<Vec<_>>().into(),
    <Char> => <>.into(),
};

//* ------------------------------- Identifiers ------------------------------ */

#[inline]
MixedId: String = { <"UpperId"> => <>.into(), <"LowerId"> => <>.into(), };

VarName: VarName = <MixedId> => VarName(<>);
CtorName: CtorName = <"CtorId"> => CtorName(<>.into());
DtorName: DtorName = <"DtorId"> => DtorName(<>.into());

NameDef<T>: NameDef<T> = <T> => NameDef(<>);
PathSeg: VarName = {
    <VarName> => <>.into(),
};
NameRef<T>: NameRef<T> = {
    <root:("root" "/")?> <path:(<VarName> "/")*> <inner:T> => {
        NameRef(root.is_some(), path, inner)
    },
};

DefId: DefId = <NameDef<Sp<VarName>>> => parser.def(<>.0);

//* ----------------------------------- Use ---------------------------------- */

UsePath: UsePath = <NameRef<UseEnum>> => UsePath(<>);
UseEnum: UseEnum = {
    <VarName> => <>.into(),
    <binder:VarName> "=" <origin:VarName> => UseAlias(<>).into(),
    "*" => UseAll.into(),
    <Uses> => <>.into(),
};
Uses: Uses = <SepByDelim<"(", ",", <UsePath>, ")">> => Uses(<>);

//* ------------------------------- Structural ------------------------------- */

Paren<T>: Paren<T> = SepByDelim<"(", ",", <T>, ")"> => Paren(<>);
App<T>: App<T> = <T*> => Appli(<>);
Ann<Tm, Ty>: Ann<Tm, Ty> = <tm:Tm> ":" <ty:Ty> => Ann {<>};
Hole: Hole = "_" => Hole;

//* --------------------------------- Pattern -------------------------------- */

PatternGen<P>: PatId = <Sp<P>> => parser.pat(<>);

#[inline]
PatId: PatId = <PatternGen<Pattern>>;
Pattern: Pattern = {
    "(" <Ann<PatId, TermId>> ")" => <>.into(),
    <Hole> => <>.into(),
    <DefId> => <>.into(),
    <ctor:CtorName> <pat:PatId> => Ctor(<>).into(),
    <Paren<PatId>> => <>.into(),
};

CoPatternGen<P>: CoPatId = <Sp<P>> => parser.copat(<>);

#[inline]
CoPatId: CoPatId = <CoPatternGen<CoPattern>>;
CoPattern: CoPattern = {
    <CoPatternAtom>,
    <x:CoPatId> <y:CoPatternAtomId> => Appli(vec![<>]).into(),
};

#[inline]
CoPatternAtomId: CoPatId = <CoPatternGen<CoPatternAtom>>;
CoPatternAtom: CoPattern = {
    <PatId> => <>.into(),
    <DtorName> => <>.into(),
};

//* ---------------------------------- Term ---------------------------------- */

TermGen<T>: TermId = <Sp<T>> => parser.term(<>);

GenBind<T>: GenBind<T> = {
    <comp:"!"?> <fix:"fix"?> <binder:PatId> <params:CoPatId?>
    <ty:(":" <TermId>)?> <bindee:T> => GenBind {
        fix: fix.is_some(),
        comp: comp.is_some(),
        binder, params, ty, bindee,
    }
};
GenBindTerm: GenBind<TermId> = GenBind<("=" <TermId>)>;
GenBindNull: GenBind<()> = GenBind<()>;

Data: Data = <arms:DataArm*> => Data {<>};
#[inline]
DataArm: DataArm = {
    "|" <name:CtorName> ":" <param:TermId> => DataArm {<>},
};
CoData: CoData = <arms:CoDataArm*> => CoData {<>};
#[inline]
CoDataArm: CoDataArm = {
    "|" <name:DtorName> <params:CoPatId?> ":" <out: TermId> => CoDataArm {<>},
};

Matcher: Matcher<PatId, TermId> = {
    "|" <binder:PatId> "->" <tail:TermId> => Matcher {<>},
};
CoMatcher: CoMatcherParam = {
    "|" <params:CoPatId> "->" <tail:TermId> => CoMatcherParam {<>},
};

#[inline]
TermAnnId: TermId = <TermGen<TermAnn>>;
TermAnn: Term = {
    <Term>,
    <Ann<TermId, TermId>> => <>.into(),
};

#[inline]
TermId: TermId = <TermGen<Term>>;
Term: Term = {
    // LALRPOP nominal semantics subtlety:
    // Indirect references (TermId) are full term references;
    // Direct ones (TermGen<Term>) are only atomic (precedence -= 1) terms.

    #[precedence(level="0")]
    // parentheses
    <Paren<TermAnnId>> => <>.into(),
    // hole and variable
    <Hole> => <>.into(),
    <NameRef<VarName>> => <>.into(),
    // thunk & force
    "{" <TermId> "}" => Thunk(<>).into(),
    "!" <TermGen<Term>> => Force(<>).into(),
    // ret
    "ret" <TermGen<Term>> => Return(<>).into(),
    // monadic block
    "monadic" <TermAnnId> "end" => MoBlock(<>).into(),
    // abstraction (alternative syntax)
    "comatch" <params:CoPatId> "->" <term:TermId> "end" => Abs(<>).into(),
    // structural types
    "data" <Data> "end" => <>.into(),
    "codata" <CoData> "end" => <>.into(),
    // constructor
    <name:CtorName> <args:TermGen<Term>> => Ctor(<>).into(),
    // match
    "match" <scrut:TermId> <arms:(<Matcher>)*> "end" => Match {<>}.into(),
    // comatch
    "comatch" <arms:(<CoMatcher>)*> "end" => CoMatchParam {<>}.into(),
    <Literal> => <>.into(),

    #[precedence(level="1")] #[assoc(side="left")]
    // application
    <term:TermGen<Term>> <arg:TermGen<Term>> => Appli(vec![<>]).into(),
    // destructor
    <term:TermGen<Term>> <name:DtorName> => Dtor(<>).into(),

    #[precedence(level="2")] #[assoc(side="right")]
    // product
    <ty_l:TermGen<Term>> "*" <ty_r:TermGen<Term>> => Prod(<>).into(),
    
    #[precedence(level="3")] #[assoc(side="right")]
    // arrow
    <ty_in:TermGen<Term>> "->" <ty_out:TermGen<Term>> => Arrow(<>).into(),

    #[precedence(level="4")]
    // pi
    "pi" <params:CoPatId> "." <term:TermGen<Term>> => Pi(<>).into(),
    // forall
    "forall" <params:CoPatId> "." <ty:TermGen<Term>> => Forall(<>).into(),
    // sigma
    "sigma" <params:CoPatId> "." <ty:TermGen<Term>> => Sigma(<>).into(),
    // exists
    "exists" <params:CoPatId> "." <ty:TermGen<Term>> => Exists(<>).into(),

    #[precedence(level="5")]
    // abstraction
    "fn" <params:CoPatId> "->" <term:TermGen<Term>> => Abs(<>).into(),
    // fixed point
    "fix" <binder:PatId> "->" <term:TermGen<Term>> => Fix(<>).into(),
    // monadically
    "monadically" <TermGen<Term>> => MoBlock(<>).into(),
    // kont call
    "do~" <body:TermGen<Term>> ";" <tail:TermGen<Term>> => KontCall {<>}.into(),
    // bind
    "do" <binder:PatId> "<-" <bindee:TermGen<Term>> ";" <tail:TermGen<Term>> => {
        Bind {<>}.into()
    },
    // pure bind
    "let" <binding:GenBindTerm> "in" <tail:TermGen<Term>> => {
        GenLet {<>}.into()
    },
};

pub SingleTerm: TermId = TermId;

//* -------------------------------- TopLevel -------------------------------- */

DataDef: DataDef = {
    "data" <name:DefId> <params:PatId*>
        <def:("where" <Data>)>
    "end" => DataDef {<>},
};

CoDataDef: CoDataDef = {
    "codata" <name:DefId> <params:PatId*>
        <def:("where" <CoData>)>
    "end" => CoDataDef {<>},
};

DeclarationGen<D>: DeclId = <Sp<D>> => parser.decl(<>);

#[inline]
DeclId: DeclId = <DeclarationGen<Modifiers<Declaration>>>;
Declaration: Declaration = {
    <DataDef> => <>.into(),
    <CoDataDef> => <>.into(),
    "define" <GenBind<("=" <TermId>)?>> "end" => Define(<>).into(),
    "alias" <GenBindTerm> "end" => Alias(<>).into(),
    "module" <name:(<NameRef<VarName>>)?>
    <top:("where" <TopLevel>)> "end" => Module {<>}.into(),
    // "use" <uses:UsePath> "where" <top:TopLevel> "end" => UseBlock {
    //     uses, top
    // }.into(),
    "main" <TermId> "end" => Exec(<>).into(),
};

Modifiers<T>: Modifiers<T> = {
    <public:"pub"?> <external:"extern"?> <inner:T> => Modifiers {
        public: public.is_some(),
        external: external.is_some(),
        inner,
    },
};

pub ReplInput: ReplInput = {
    <DeclId> => <>.into(),
    <TermId> => <>.into(),
};

pub TopLevel: TopLevel = {
    <DeclId*> => TopLevel(<>),
};

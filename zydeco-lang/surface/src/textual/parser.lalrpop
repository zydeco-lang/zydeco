use crate::textual::{
    syntax::*, lexer::*, escape,
};

grammar<'input, 'ctx>(
    input: &'input str,
    loc: &'input LocationCtx,
    parser: &'ctx mut Parser,
);

//* --------------------------------- Tokens --------------------------------- */

extern {
    type Location = usize;
    
    enum Tok<'input> {
        "UpperId" => Tok::UpperIdent(<&'input str>),
        "LowerId" => Tok::LowerIdent(<&'input str>),
        "CtorId" => Tok::CtorIdent(<&'input str>),
        "DtorId" => Tok::DtorIdent(<&'input str>),

        "where" => Tok::Where,
        "end" => Tok::End,
        "module" => Tok::Module,
        "use" => Tok::Use,
        "pub" => Tok::Public,
        "extern" => Tok::Extern,
        "data" => Tok::Data,
        "codata" => Tok::Codata,
        "alias" => Tok::Alias,
        "define" => Tok::Define,
        "main" => Tok::Main,
        "let" => Tok::Let,
        "in" => Tok::In,
        "do" => Tok::Do,
        "ret" => Tok::Ret,
        "fn" => Tok::Fn,
        "pi" => Tok::Pi,
        "rec" => Tok::Rec,
        "match" => Tok::Match,
        "comatch" => Tok::Comatch,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "@" => Tok::At,
        "pack" => Tok::Pack,

        "NumLit" => Tok::NumLit(<&'input str>),
        "StrLit" => Tok::StrLit(<&'input str>),
        "CharLit" => Tok::CharLit(<&'input str>),

        "(" => Tok::ParenOpen,
        ")" => Tok::ParenClose,
        "[" => Tok::BracketOpen,
        "]" => Tok::BracketClose,
        "{" => Tok::BraceOpen,
        "}" => Tok::BraceClose,
        "," => Tok::Comma,
        ":" => Tok::Colon,
        "::" => Tok::ColonColon,
        "=" => Tok::Equals,
        ";" => Tok::Semicolon,
        "!" => Tok::Force,
        "/" => Tok::Slash,
        "|" => Tok::Branch,
        "+" => Tok::Plus,
        "." => Tok::Dot,
        ".." => Tok::DotDot,
        "->" => Tok::Arrow,
        "<-" => Tok::Assign,
        "_" => Tok::Hole,
    }
}

//* -------------------------------- Utilities ------------------------------- */

#[inline]
Sp<T>: Sp<T> = <l:@L> <inner:T> <r:@R> => Sp {
    info: Span::new(l, r).under_loc_ctx(loc), inner,
};

// #[inline]
SepByDelim<Start, Sep, T, End>: Vec<T> = {
    Start <mut v: (<T> Sep)*> <last: T?> End => {
        match last {
            None => { },
            Some(t) => { v.push(t); }
        };
        v
    }
};

//* -------------------------------- Terminals ------------------------------- */

Int: i64 = <s:"NumLit"> => s.parse().unwrap();
String: String = <s:"StrLit"> => escape::apply_string_escapes(&s[1..s.len()-1]);
Char: char = <s:"CharLit"> => escape::apply_char_escapes(s);

Literal: Literal = {
    <Int> => <>.into(),
    <String> => <>.into(),
    <Char> => <>.into(),
};

//* ------------------------------- Identifiers ------------------------------ */

#[inline]
UpperId: String = "UpperId" => <>.into();
#[inline]
LowerId: String = "LowerId" => <>.into();
#[inline]
MixedId: String = { <UpperId>, <LowerId>, };
#[inline]
CtorId: String = "CtorId" => <>.into();
#[inline]
DtorId: String = "DtorId" => <>.into();

#[inline]
ModName: ModName = <MixedId> => ModName(<>);
#[inline]
VarName: VarName = <MixedId> => VarName(<>);
CtorName: CtorName = {
    <CtorId> => CtorName(<>),
    "pack" => CtorName(String::from("pack")),
};
DtorName: DtorName = {
    <DtorId> => DtorName(<>),
    "@" => DtorName(String::from("@")),
};

NameDef<T>: NameDef<T> = <T> => NameDef(<>);
NameRef<T>: NameRef<T> = <path:(<ModName> "/")*> <inner:T> => {
    NameRef(path, inner)
};

DefId: DefId = <NameDef<Sp<VarName>>> => parser.def(<>.0);

//* ------------------------------- Structural ------------------------------- */

Paren<T>: Paren<T> = SepByDelim<"(", ",", <T>, ")"> => Paren(<>);
App<T>: App<T> = <T*> => App(<>);
Annotation<Tm, Ty>: Annotation<Tm, Ty> = <tm:Tm> ":" <ty:Ty> => {
    Annotation { tm, ty, }
};
Hole: Hole = "_" => Hole;

//* --------------------------------- Pattern -------------------------------- */

#[inline]
PatternGen<P>: PatternId = <Sp<P>> => parser.pat(<>);

#[inline]
PatternId: PatternId = <PatternGen<Pattern>>;
Pattern: Pattern = {
    "(" <Annotation<PatternId, TermId>> ")" => <>.into(),
    <Hole> => <>.into(),
    <DefId> => <>.into(),
    <ctor:CtorName> <pat:PatternId> => {
        Ctor(ctor, pat).into()
    },
    <Paren<PatternId>> => <>.into(),
};

#[inline]
CoPatternGen<P>: CoPatternId = <Sp<P>> => parser.copat(<>);

#[inline]
CoPatternId: CoPatternId = <CoPatternGen<CoPattern>>;
CoPattern: CoPattern = {
    <CoPatternAtom>,
    <x:CoPatternAtomId> <y:CoPatternId> => {
        App(vec![x, y]).into()
    },
    // <CoPatternAtomId*> => App(c).into(),
};

#[inline]
CoPatternAtomId: CoPatternId = <CoPatternGen<CoPatternAtom>>;
CoPatternAtom: CoPattern = {
    <PatternId> => <>.into(),
    <DtorName> => {
        <>.into()
    },
};

//* ---------------------------------- Term ---------------------------------- */

GenBindTerm: GenBind<TermId> = {
    <rec:"rec"?> <comp:"!"?> <binder:PatternId> <params:CoPatternId?>
    <ty:(":" <TermId>)?> <bindee: ("=" <TermId>)> => GenBind {
        rec: rec.is_some(),
        comp: comp.is_some(),
        binder, params, ty, bindee,
    }
};
GenBindNull: GenBind<()> = {
    <rec:"rec"?> <comp:"!"?> <binder:PatternId> <params:CoPatternId?>
    <ty:(":" <TermId>)?> <bindee: ()> => GenBind {
        rec: rec.is_some(),
        comp: comp.is_some(),
        binder, params, ty, bindee,
    }
};

Data: Data = <DataArm*> => Data { arms: <> };
#[inline]
DataArm: DataArm = {
    "|" <name:CtorName> <param:TermId> => {
        DataArm { name, param }
    },
};
CoData: CoData = <CoDataArm*> => CoData { arms: <> };
#[inline]
CoDataArm: CoDataArm = {
    "|" <name:DtorName> <params:CoPatternId?> ":" <out: TermId> => {
        CoDataArm { name, params, out }
    },
};

Matcher: Matcher<TermId> = {
    "|" <binder:PatternId> "->" <tail:TermId> => Matcher {
        binder, tail
    },
};
CoMatcher: CoMatcher<TermId> = {
    "|" <params:CoPatternId> "->" <tail:TermId> => CoMatcher {
        params, tail
    },
};

#[inline]
TermGen<T>: TermId = <Sp<T>> => parser.term(<>);

#[inline]
TermId: TermId = <TermGen<Term>>;
Term: Term<NameRef<VarName>> = {
    <TermRight>,
    // abstraction
    "fn" <params:CoPatternId> "->"
        <term:TermId> => Abs(params, term).into(),
    // pi
    "pi" <params:CoPatternId> "->"
        <term:TermId> => Pi(params, term).into(),
    // forall
    "forall" <params:CoPatternId> "." <ty:TermRightId> =>
        Forall(params, ty).into(),
    // exists
    "exists" <params:CoPatternId> "." <ty:TermRightId> =>
        Exists(params, ty).into(),
    // recursion
    "rec" <binder:PatternId> "->"
        <term:TermId> => Rec(binder, term).into(),
    // bind
    "do" <binder:PatternId> "<-" <bindee:TermId> ";" <tail:TermId> => Bind {
        binder, bindee, tail
    }.into(),
    // pure bind
    "let" <binding:GenBindTerm> "in" <tail:TermId> => PureBind {
        binding, tail
    }.into(),
    // use bind
    "use" <uses:UsePath> "in" <tail:TermId> => UseBind {
        uses, tail
    }.into(),
    "data" <Data> "end" => <>.into(),
    "codata" <CoData> "end" => <>.into(),
    // match
    "match" <scrut:TermId> <arms:(<Matcher>)*> "end" => Match {
        scrut, arms
    }.into(),
    "comatch" <arms:(<CoMatcher>)*> "end" => CoMatch {
        arms
    }.into(),
};

#[inline]
TermAnnId: TermId = <TermGen<TermAnn>>;
TermAnn: Term<NameRef<VarName>> = {
    <Term>,
    <Annotation<TermId, TermId>> => <>.into(),
};

#[inline]
TermParenId: TermId = <TermGen<TermParen>>;
TermParen: Term<NameRef<VarName>> = <Paren<TermAnnId>> => <>.into();

#[inline]
TermRightId: TermId = <TermGen<TermRight>>;
TermRight: Term<NameRef<VarName>> = {
    <TermAtom>,
    // application
    <term:TermRightId> <arg:TermAtomId> => {
        App(vec![term, arg]).into()
    },
    // arrow
    <ty_in:TermRightId> "->" <ty_out:TermAtomId> => {
        Arrow(ty_in, ty_out).into()
    },
    // destructor
    <term:TermRightId> <name:DtorName> => {
        Dtor(term, name).into()
    },
};

#[inline]
TermAtomId: TermId = <TermGen<TermAtom>>;
TermAtom: Term<NameRef<VarName>> = {
    // paren
    <TermParen>,
    <Hole> => <>.into(),
    <NameRef<VarName>> => Term::Var(<>),
    "{" <TermId> "}" => Thunk(<>).into(),
    "!" <TermAtomId> => Force(<>).into(),
    "ret" <TermAtomId> => Return(<>).into(),
    // constructor
    <name:CtorName> <args:TermAtomId> => {
        Ctor(name, args).into()
    },
    <Literal> => <>.into(),
};

pub SingleTerm: TermId = TermId;

//* -------------------------------- TopLevel -------------------------------- */

DataDef: DataDef = {
    "data" <name:DefId> <params:PatternId*>
        <def:("where" <Data>)?>
    "end" => DataDef {
        name, params, def,
    }
};

CoDataDef: CoDataDef = {
    "codata" <name:DefId> <params:PatternId*>
        <def:("where" <CoData>)?>
    "end" => CoDataDef {
        name, params, def,
    }
};

DefineHead: () = { "define", "alias", };
DefineEnd: () = { "end", ";", };

ModuleHead: NameDef<ModName> = "module" <NameDef<ModName>>;
ModuleEnd: () = "end";

UseEnum: UseEnum = {
    <VarName> => <>.into(),
    <binder:VarName> "=" <origin:VarName> => UseAlias(binder, origin).into(),
    ".." => UseAll.into(),
    <Uses> => <>.into(),
};
UsePath: UsePath = <NameRef<UseEnum>> => UsePath(<>);
Uses: Uses = <SepByDelim<"(", ",", <UsePath>, ")">> => Uses(<>);

Declaration: Declaration = {
    <DataDef> => <>.into(),
    <CoDataDef> => <>.into(),
    DefineHead <GenBindTerm> DefineEnd => Define(<>).into(),
    <name:ModuleHead> <top:("where" <TopLevel>)?> ModuleEnd => {
        Module { name, top }.into()
    },
    "extern" <GenBindNull> "end" => Extern(<>).into(),
    "use" <UsePath> "end" => UseDef(<>).into(),
    "use" <uses:UsePath> "where" <top:TopLevel> "end" => UseBlock {
        uses, top
    }.into(),
    "main" <TermId> "end" => Main(<>).into(),
};

Modifiers<T>: Modifiers<T> = {
    <public:"pub"?> <inner:T> => Modifiers {
        public: public.is_some(),
        inner,
    },
};

pub ReplInput: ReplInput = {
    <Modifiers<Declaration>> => <>.into(),
    <TermId> => <>.into(),
};

pub TopLevel: TopLevel = {
    <Modifiers<Declaration>*> => TopLevel(<>),
};

use crate::{
    surface::parse::{syntax::*, lexer::*, escape},
    syntax::DeclSymbol,
    utils::span::*,
};

grammar<'input>(input: &'input str);

//* --------------------------------- Module --------------------------------- */

pub Zydeco: Sp<TopLevel> = Sp<TopLevel>;

TopLevel: TopLevel = {
    <declarations:DeclSymbol<Declaration>*> => {
        TopLevel {
            declarations: declarations,
        }
    },
};

DeclSymbol<T>: DeclSymbol<T> = {
    <public:"pub"?> <external:"extern"?> <inner:T> => DeclSymbol {
        public: public.is_some(),
        external: external.is_some(),
        inner,
    }
};

Declaration: Declaration = {
    "module" <name:NameRef<UpperId>?> "where"
        <declarations:DeclSymbol<Declaration>*>
    "end" => Module {
        name, declarations
    }.into(),
    "use" <UseDef> "end" => <>.into(),
    "data" <name:NameDef<UpperId>> <params:TypePattern*>
        <ctors:("where" <DataBr*>)?>
    "end" => Data {
        name, params, ctors: ctors.unwrap_or_default(),
    }.into(),
    "codata" <name:NameDef<UpperId>> <params:TypePattern*>
        <dtors:("where" <CodataBr*>)?>
    "end" => Codata {
        name, params, dtors: dtors.unwrap_or_default(),
    }.into(),
    "alias" <name:NameDef<UpperId>> <params:TypePattern*> "=" <ty:Box<Sp<Type>>> "end" => Alias {
        name, params, ty,
    }.into(),
    "define" <gen:GenLet> DefineEnd => Define(gen).into(),
    "main" <entry:Sp<TermComputation>> "end" => Main {
        entry,
    }.into(),
};

UseDef: UseDef = {
    <NameRef<MixedId>> => <>.into(),
    ".." => UseAll.into(),
    <path:NameRef<UpperId>>  <cluster:SepByDelim<"(", ",", UseDef, ")">> => UseCluster {
        path, cluster,
    }.into(),
};

DefineEnd: () = {
    "end" => (),
    ";" => (),
};

DataBr: DataBr<CtorV, Sp<Type>> = {
    "|" <ctorv:CtorV> <tys:SepByDelim<"(", ",", <Sp<Type>>, ")">> => {
        DataBr { ctorv, tys }
    }
};

CodataBr: CodataBr<DtorV, Sp<Type>> = {
    "|" <dtorv:DtorV> ":" <ty:Sp<Type>> => {
        CodataBr { dtorv, ty }
    }
};

//* --------------------------------- Pattern -------------------------------- */

TypePattern: TypePattern = {
    "(" <name:NameDef<UpperId>> <kind: (":" <Sp<Kind>>)?> ")" => (name, kind),
    <NameDef<UpperId>> => (<>, None),
};

TermPattern: TermPattern = {
    "(" <v:NameDef<LowerId>> <ty:(":" <Sp<Type>>)?> ")" => (v, ty),
    <NameDef<LowerId>> => (<>, None),
};

Pattern: Pattern = {
    <TypePattern> => <>.into(),
    <TermPattern> => <>.into(),
};

//* ---------------------------------- Kind ---------------------------------- */

Kind: Kind = {
    KindArrow,
};

KindArrow: Kind = {
    <k:Box<Sp<KindAtom>>> "->" <kd:Box<Sp<KindArrow>>> => {
        Arrow(k, kd).into()
    },
    KindAtom,
};

KindAtom: Kind = {
    "(" <Kind> ")" => <>,
    "VType" => KindBase::VType.into(),
    "CType" => KindBase::CType.into(),
};

//* ---------------------------------- Type ---------------------------------- */

Type: Type = {
    "forall" <param:TypePattern+> "." <ty:Box<Sp<TypeArrow>>> => {
        Forall { param, ty }.into()
    },
    "exists" <param:TypePattern+> "." <ty:Box<Sp<TypeArrow>>> => {
        Exists { param, ty }.into()
    },
    "type" <params:TypePattern+> "." <body:Box<Sp<TypeArrow>>> => {
        TypeAbs { params, body }.into()
    },
    TypeArrow,
};

TypeArrow: Type = {
    <t:Box<Sp<TypeApp>>> "->" <ty:Box<Sp<Type>>> => {
        Arrow(t, ty).into()
    },
    TypeApp,
};

TypeApp: Type = {
    <t:Box<Sp<TypeApp>>> <ty:Box<Sp<TypeAtom>>> => {
        TypeApp(t, ty).into()
    },
    TypeAtom,
};

TypeAtom: Type = {
    "(" <Type> ")" => <>,
    <NameRef<UpperId>> => <>.into(),
    "_?" => Hole.into(),
};

//* ---------------------------------- Term ---------------------------------- */

pub TermSpan: Sp<Term> = Sp<Term>;

Term: Term = {
    <TermComputation> => <>.into(),
    <TermValue> => <>.into(),
};

TermValue: TermValue = {
    "(" <TermValue> ")" => <>.into(),
    "(" <tv:Box<Sp<TermValue>>> ":" <ty:Sp<Type>> ")" => Annotation {
        term: tv,
        ty,
    }.into(),
    <NameRef<LowerId>> => <>.into(),
    "{" <tc:Box<Sp<TermComputation>>> "}" => Thunk(tc).into(),
    <ctorv:CtorV> <args:SepByDelim<"(", ",", Sp<TermValue>, ")">> => Ctor {
        ctorv, args,
    }.into(),
    <Literal> => <>.into(),
    "pack" "(" <ty:Box<Sp<Type>>> "," <body:Box<Sp<TermValue>>> ")" => Pack {
        ty, body,
    }.into(),
};

TermComputation: TermComputation = {
    TermComputationRight,
    "let" <gen:GenLet> LetSep <body:Box<Sp<TermComputation>>> => Let {
        gen, body,
    }.into(),
    "do" <var:TermPattern> "<-"
        <comp:Box<Sp<TermComputation>>> ";"
    <body:Box<Sp<TermComputation>>> => Do {
        var, comp, body,
    }.into(),
    "do" <body:Box<Sp<TermComputation>>> ";" <kont:Box<Sp<TermComputation>>> => App {
        body,
        arg: {
            let sp = kont.info.clone();
            let v: Box<Sp<TermValue>> = Box::new(sp.make(Thunk(kont).into()));
            v
        },
    }.into(),
    "rec" <var:TermPattern> "->" <body:Box<Sp<TermComputation>>> => Rec {
        var, body,
    }.into(),
    "match" <scrut:Box<Sp<TermValue>>> <arms:MatchBr*> "end" => Match {
        scrut, arms,
    }.into(),
    "comatch" <arms:ComatchBr*> "end" => Comatch {
        arms,
    }.into(),
    "fn" <param:Pattern*> "->" <body:Box<Sp<TermComputation>>> => Abs {
        param, body,
    }.into(),
    "fn" <dtorv:DtorV> "->" <body:Sp<TermComputation>> => Comatch {
        arms: vec![Comatcher { dtorv, body }],
    }.into(),
    "match" <scrut:Box<Sp<TermValue>>> 
    "|"? "pack" "(" <tvar:NameDef<UpperId>> "," <var:NameDef<LowerId>> ")" "->"
        <body:Box<Sp<TermComputation>>> 
    "end" => MatchPack {
        scrut, tvar, var, body,
    }.into(),
};

LetSep: () = {
    ";" => (),
    "in" => (),
};

TermComputationRight: TermComputation = {
    TermComputationAtom,
    <body:Box<Sp<TermComputationRight>>> <arg:Box<Sp<TermValue>>> => App {
        body, arg,
    }.into(),
    <body:Box<Sp<TermComputationRight>>> "@" "(" <arg:Box<Sp<Type>>> ")" => App {
        body, arg,
    }.into(),
    <body:Box<Sp<TermComputationRight>>> <dtorv:DtorV> => Dtor {
        body, dtorv,
    }.into(),
};

TermComputationAtom: TermComputation = {
    "(" <TermComputation> ")" => <>.into(),
    "(" <tc:Box<Sp<TermComputation>>> ":" <ty:Sp<Type>> ")" => Annotation {
        term: tc, ty,
    }.into(),
    "ret" <Box<Sp<TermValue>>> => Ret(<>).into(),
    "!" <Box<Sp<TermValue>>> => Force(<>).into(),
};

GenLet: GenLet = {
    <rec:"rec"?> <fun:"fn"?> <var:NameDef<LowerId>> <params:Pattern*>
    <ty:(":" <Sp<Type>>)?> <def: ("=" <Box<Sp<Term>>>)?> => GenLet {
        rec: rec.is_some(),
        fun: fun.is_some(),
        name: (var, ty),
        params,
        def,
    }
};

MatchBr: Matcher<CtorV, NameDef, Sp<TermComputation>> = {
    "|" <ctorv:CtorV> <vars:SepByDelim<"(", ",", NameDef<LowerId>, ")">> "->"
    <body:Sp<TermComputation>> => Matcher {
        ctorv, vars, body
    }
};

ComatchBr: Comatcher<DtorV, Sp<TermComputation>> = {
    "|" <dtorv:DtorV> "->"
    <body:Sp<TermComputation>> => Comatcher {
        dtorv, body
    }
};

Literal: Literal = {
    <Int> => <>.into(),
    <String> => <>.chars().collect::<Vec<_>>().into(),
    <Char> => <>.into(),
};

//* ------------------------------- Identifiers ------------------------------ */

NameDef<T>: NameDef = {
    Sp<(<Sp<T>>)> => NameDef {
        ident: <>.inner, info: <>.info,
    },
};

NameRef<T>: NameRef = {
    Sp<(<(<Sp<UpperId>> ".")*> <Sp<T>>)> => NameRef {
        path: <>.inner.0, ident: <>.inner.1, info: <>.info,
    },
};

CtorV: CtorV = <Sp<UpperId>> => <>.into();
DtorV: DtorV = "." <Sp<LowerId>> => <>.into();

MixedId: String = {
    <UpperId> => <>,
    <LowerId> => <>,
};

UpperId: String = "UpperId" => <>.into();
LowerId: String = "LowerId" => <>.into();

//* -------------------------------- Terminals ------------------------------- */

Int: i64 = <s:"NumLit"> => s.parse().unwrap();
String: String = <s:"StrLit"> => escape::apply_string_escapes(&s[1..s.len()-1]);
Char: char = <s:"CharLit"> => escape::apply_char_escapes(s);

//* -------------------------------- Utilities ------------------------------- */

#[inline]
Sp<T>: Sp<T> = {
    <l:@L> <inner:T> <r:@R> => Span::new(l,r).make(inner)
};

#[inline]
Box<T>: Box<T> = {
    <inner:T> => Box::new(inner)
};

#[inline]
SepByDelim<Start, Sep, T, End>: Vec<T> = {
    Start <mut v: (<T> Sep)*> <last: T?> End => {
        match last {
            None => { },
            Some(t) => { v.push(t); }
        };
        v
    }
};

//* --------------------------------- Tokens --------------------------------- */

extern {
    type Location = usize;
    
    enum Tok<'input> {
        "UpperId" => Tok::UpperIdent(<&'input str>),
        "LowerId" => Tok::LowerIdent(<&'input str>),

        "where" => Tok::Where,
        "end" => Tok::End,
        "module" => Tok::Module,
        "use" => Tok::Use,
        "pub" => Tok::Pub,
        "extern" => Tok::Extern,
        "data" => Tok::Data,
        "codata" => Tok::Codata,
        "alias" => Tok::Alias,
        "define" => Tok::Define,
        "main" => Tok::Main,
        "let" => Tok::Let,
        "in" => Tok::In,
        "do" => Tok::Do,
        "ret" => Tok::Ret,
        "fn" => Tok::Fn,
        "type" => Tok::Type,
        "rec" => Tok::Rec,
        "match" => Tok::Match,
        "comatch" => Tok::Comatch,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "@" => Tok::At,
        "pack" => Tok::Pack,

        "NumLit" => Tok::NumLit(<&'input str>),
        "StrLit" => Tok::StrLit(<&'input str>),
        "CharLit" => Tok::CharLit(<&'input str>),

        "VType" => Tok::VType,
        "CType" => Tok::CType,

        "(" => Tok::ParenOpen,
        ")" => Tok::ParenClose,
        "[" => Tok::BracketOpen,
        "]" => Tok::BracketClose,
        "{" => Tok::BraceOpen,
        "}" => Tok::BraceClose,
        "," => Tok::Comma,
        ":" => Tok::Colon,
        "=" => Tok::Equals,
        ";" => Tok::Semicolon,
        "!" => Tok::Force,
        "|" => Tok::Branch,
        "." => Tok::Dot,
        ".." => Tok::DotDot,
        "->" => Tok::Arrow,
        "<-" => Tok::Assign,
        "_?" => Tok::Hole,
    }
}

